plugins {
    id("com.android.library")
}

ext.pluginVersionCode = 1
ext.pluginVersionName = "3.6.2"
ext.zipName = "godot-notifications"
ext.godotVersion = "3.6.2"

android {
    namespace "com.onecat.godotnotification"
    compileSdk 35
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35
        versionCode pluginVersionCode
        versionName pluginVersionName
        consumerProguardFiles("proguard-rules.pro")
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }
    ndkVersion '28.1.13356709'
    buildToolsVersion '35.0.0'
    libraryVariants.configureEach { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "GodotNotification.$pluginVersionName.${variant.name}.aar"
        }
    }
}

dependencies {
    // Getting Godot dependency locally
    //compileOnly fileTree(dir:"libs", include:["godot-lib.release.aar"])
    // Intended way of getting Godot dependencies
    compileOnly 'org.godotengine:godot:3.6.2.stable'
    implementation 'androidx.work:work-runtime:2.11.0'
}

tasks.register('buildIntoRelease', Copy) {
    group = "build"
    dependsOn tasks.build
    from([buildDir.absolutePath, 'outputs', 'aar'].join(File.separator))
    include("GodotNotification.${pluginVersionName}.release.aar")
    into([rootDir.absolutePath, 'release'].join(File.separator))
}

tasks.register('buildIntoReleaseAndPrepareForGithub', Zip) {
    group = "build"
    dependsOn tasks.buildIntoRelease
    from([rootDir.absolutePath, 'release'].join(File.separator))
    include("*.gd", "*.gdap", "*.aar")
    archiveFileName = "${zipName}-${godotVersion}.zip"
    destinationDirectory = file([rootDir.absolutePath, 'release'].join(File.separator))
    def metaTxt = new File([rootDir.absolutePath, 'release', "github-meta.txt"].join(File.separator))
    metaTxt.text = "Godot ${pluginVersionName}, rev.${pluginVersionCode}"
    metaTxt.text += "\nGD${godotVersion}rev${pluginVersionCode}"
}
